apply plugin: 'com.android.library'

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        minSdkVersion 7
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    def artifactSupportV4 = 'com.android.support:support-v4:+'
    def artifactSupportRecyclerViewV7 = 'com.android.support:recyclerview-v7:+'
    def artifactSupportAnnotations = 'com.android.support:support-annotations:21.+'

    def useLocalV4 = false
    def useLocalAnnotations = false

    def localV4Project = ':support-v4'
    def localAnnotations = ':support-annotations'

    allprojects {
        rootProject.allprojects.project.each {
            if (it.path == localV4Project) {
                useLocalV4 = true
            }
            else if (it.path == localAnnotations) {
                useLocalAnnotations = true
            }
        }
    }

    if (useLocalV4) {
        println 'Using local support-v4 library in ' + project.path
        compile project(localV4Project)

        if (useLocalAnnotations) {
            println 'Using local support-annotations library in ' + project.path
            compile project(localAnnotations)
        } else {
            compile artifactSupportAnnotations
        }

        compile (artifactSupportRecyclerViewV7) {
            exclude group: 'com.android.support', module: 'support-v4'
            exclude group: 'com.android.support', module: 'support-annotations'
        }

    } else {
        compile artifactSupportV4
        compile artifactSupportRecyclerViewV7
        compile artifactSupportAnnotations
    }
}
